apply plugin: "com.jfrog.bintray"
apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

version = '1.0.0'
group = 'com.facebook.yoga.android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets.main {
        jni.srcDirs = [] // disalbe NDK auto build (not sure why this is necessary)
        // The alternative, fat-aar does an equivalent thing to this hack
        // seehttps://github.com/adwiv/android-fat-aar/blob/master/fat-aar.gradle#L307
        jniLibs.srcDirs = ['build/intermediates/exploded-aar/com.facebook.yoga/yoga/1.0.0/jni']
    }
}

dependencies {
    compile project(':yoga')
}

task sourcesJar(type: Jar) {
    classifier = 'source'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

ext {
    bintrayName = "com.facebook.yoga.android:yoga-layout"
}

apply from: rootProject.file('gradle/android-jcenter-install.gradle')
